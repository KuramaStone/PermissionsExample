plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.2'
    id("io.papermc.paperweight.userdev") version "1.7.3"
}

group = 'com.github.kuramastone'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url 'https://repo.codemc.io/repository/maven-releases/' }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT")
    paperweight.paperDevBundle("1.21.1-R0.1-SNAPSHOT")
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '5.1.0'
    implementation 'com.h2database:h2:2.3.232'
    compileOnly 'com.github.retrooper:packetevents-spigot:2.4.0'
    compileOnly 'me.clip:placeholderapi:2.11.6'


    testCompileOnly("io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT")
    testImplementation group: 'com.zaxxer', name: 'HikariCP', version: '5.1.0'
    testImplementation 'com.h2database:h2:2.3.232'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    manifest {
        attributes["paperweight-mappings-namespace"] = "spigot"
    }

    relocate 'com/zaxxer/hikari', "${project.group}.shade.com.zaxxer.hikari"
    destinationDirectory = file(System.getenv("minecraft_servers") + "\\1.21.1 Paper\\plugins")
    archiveClassifier = ''
    archiveFileName = "${project.name}-${version}.jar"
}